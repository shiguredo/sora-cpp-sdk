plugins {
    id 'com.android.application'
}

// DEPS ファイルから CMAKE_VERSION を読み込む
def depsFile = file('../../../DEPS')
if (!depsFile.exists()) {
    throw new GradleException("DEPS ファイルが見つかりません: ${depsFile.absolutePath}")
}
def props = new Properties()
depsFile.withInputStream { props.load(it) }
def cmakeVersion = props.getProperty('CMAKE_VERSION')
if (!cmakeVersion) {
    throw new GradleException("DEPS ファイルに CMAKE_VERSION が見つかりません")
}

def soraAndroidAbi = project.findProperty("SORA_ANDROID_ABI") ?: "arm64-v8a"
def soraClangLibArch = (soraAndroidAbi == "x86_64") ? "x86_64" : "aarch64"
def soraAndroidPlatformDir = (soraAndroidAbi == "x86_64") ? "android_x86_64" : "android"
def localWebrtcBuildDir = project.findProperty("SORA_WEBRTC_LOCAL_BUILD_DIR")
def useLocalWebrtc = localWebrtcBuildDir != null && localWebrtcBuildDir.trim()
def webrtcIncludeDir = useLocalWebrtc ?
        "${localWebrtcBuildDir}/_source/${soraAndroidPlatformDir}/webrtc/src" :
        "$projectDir/../../../_install/${soraAndroidPlatformDir}/release/webrtc/include"
def webrtcLibraryDir = useLocalWebrtc ?
        "${localWebrtcBuildDir}/_build/${soraAndroidPlatformDir}/release/webrtc" :
        "$projectDir/../../../_install/${soraAndroidPlatformDir}/release/webrtc/lib/${soraAndroidAbi}"
def llvmClangDir = useLocalWebrtc ?
        "${localWebrtcBuildDir}/_source/${soraAndroidPlatformDir}/webrtc/src/third_party/llvm-build/Release+Asserts" :
        "$projectDir/../../../_install/${soraAndroidPlatformDir}/release/llvm/clang"
def libcxxIncludeDir = useLocalWebrtc ?
        "${localWebrtcBuildDir}/_source/${soraAndroidPlatformDir}/webrtc/src/third_party/libc++/src/include" :
        "$projectDir/../../../_install/${soraAndroidPlatformDir}/release/llvm/libcxx/include"
def webrtcJarPath = useLocalWebrtc ?
        "${localWebrtcBuildDir}/_build/${soraAndroidPlatformDir}/release/webrtc/${soraAndroidAbi}/lib.java/sdk/android/libwebrtc.jar" :
        "$projectDir/../../../_install/${soraAndroidPlatformDir}/release/webrtc/jar/webrtc.jar"

android {
    namespace "jp.shiguredo.hello"
    compileSdk 34

    defaultConfig {
        applicationId "jp.shiguredo.hello"
        minSdk 29
        targetSdk 34
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                cppFlags '-std=gnu++20'
                //arguments "-DSORA_DIR=$projectDir/../../../_install/android/debug/sora",
                //    "-DCMAKE_BUILD_TYPE=Debug",
                //    "-DBOOST_ROOT=$projectDir/../../../_install/android/debug/boost",
                //    // "-DWEBRTC_INCLUDE_DIR=$projectDir/../../../_install/android/debug/webrtc/include",
                //    // "-DWEBRTC_LIBRARY_DIR=$projectDir/../../../_install/android/debug/webrtc/lib/arm64-v8a",
                //    // "-DLIBCXX_INCLUDE_DIR=$projectDir/../../../_install/android/debug/llvm/libcxx/include",
                //    "-DWEBRTC_INCLUDE_DIR=$projectDir/../../../_source/android/debug/webrtc/src",
                //    "-DWEBRTC_LIBRARY_DIR=$projectDir/../../../_build/android/debug/webrtc",
                //    "-DLIBCXX_INCLUDE_DIR=$projectDir/../../../_source/android/debug/webrtc/src/buildtools/third_party/libc++/trunk/include",
                arguments "-DCMAKE_TOOLCHAIN_FILE=$projectDir/../../../cmake/android.toolchain.cmake",
                    "-DANDROID_OVERRIDE_TOOLCHAIN_FILE=$projectDir/../../../_install/${soraAndroidPlatformDir}/release/android-ndk/build/cmake/android.toolchain.cmake",
                    "-DANDROID_OVERRIDE_C_COMPILER=${llvmClangDir}/bin/clang",
                    "-DANDROID_OVERRIDE_CXX_COMPILER=${llvmClangDir}/bin/clang++",
                    "-DSORA_DIR=$projectDir/../../../_install/${soraAndroidPlatformDir}/release/sora",
                    "-DCMAKE_BUILD_TYPE=Release",
                    "-DBOOST_ROOT=$projectDir/../../../_install/${soraAndroidPlatformDir}/release/boost",
                    "-DBLEND2D_ROOT_DIR=$projectDir/../../../_install/${soraAndroidPlatformDir}/release/blend2d",
                    "-DWEBRTC_INCLUDE_DIR=${webrtcIncludeDir}",
                    "-DWEBRTC_LIBRARY_DIR=${webrtcLibraryDir}",
                    "-DLIBCXX_INCLUDE_DIR=${libcxxIncludeDir}",
                    "-DLLVM_DIR=${llvmClangDir}",
                    // "-DWEBRTC_INCLUDE_DIR=$projectDir/../../../../webrtc-build/_source/android/webrtc/src",
                    // "-DWEBRTC_LIBRARY_DIR=$projectDir/../../../../webrtc-build/_build/android/release/webrtc/${soraAndroidAbi}",
                    // "-DLIBCXX_INCLUDE_DIR=$projectDir/../../../../webrtc-build/_source/android/webrtc/src/third_party/libc++/src/include",
                    // "-DLLVM_DIR=$projectDir/../../../../webrtc-build/_source/android/webrtc/src/third_party/llvm-build/Release+Asserts",
                    "-DANDROID_STL=none",
                    "-DANDROID_NATIVE_API_LEVEL=29",
                    "-DANDROID_PLATFORM=29",
                    "-DANDROID_ABI=${soraAndroidAbi}",
                    "-DANDROID_CPP_FEATURES=exceptions rtti",
                    "-DANDROID_NDK=$projectDir/../../../_install/${soraAndroidPlatformDir}/release/android-ndk",
                    "-DCMAKE_ANDROID_EXCEPTIONS=ON",
                    "-DCMAKE_EXE_LINKER_FLAGS=-L$projectDir/../../../_install/${soraAndroidPlatformDir}/release/android-ndk/toolchains/llvm/prebuilt/linux-x86_64/lib/clang/19/lib/linux/${soraClangLibArch}",
                    "-DCMAKE_SHARED_LINKER_FLAGS=-L$projectDir/../../../_install/${soraAndroidPlatformDir}/release/android-ndk/toolchains/llvm/prebuilt/linux-x86_64/lib/clang/19/lib/linux/${soraClangLibArch}"
            }
            ndk {
                abiFilters soraAndroidAbi
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version cmakeVersion
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation files(webrtcJarPath)
}
